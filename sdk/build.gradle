apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 8
        versionName "1.4.0-beta"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'org.apache.httpcomponents:httpclient-android:4.3.5.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.0-rc3'
    compile 'com.google.android.gms:play-services-gcm:7.5.0'
}

//apply from: 'https://raw.githubusercontent.com/blundell/release-android-library/master/android-release-aar.gradle'
// ./gradlew clean build generateRelease
apply plugin: 'maven'

def sdkGroupId = 'ir.mbaas'
def sdkArtifactId = 'sdk'
def sdkVersion = android.defaultConfig.versionName
def siteUrl = 'http://www.mbaas.ir/'
def gitUrl = 'http://git.softwaretalks.ir/mbaas/PushNotification.git'

def localReleaseDest = "${buildDir}/release/${sdkVersion}"

task androidJavadocs(type: Javadoc) {
    failOnError = false
    source = android.sourceSets.main.java.srcDirs
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

uploadArchives {
    repositories.mavenDeployer {
        pom.project {
            //packaging 'aar'

            groupId sdkGroupId
            artifactId sdkArtifactId
            version sdkVersion

            // Add your description here
            name 'MBaaS SDK'
            description = 'SDK for mbaas.ir'
            url siteUrl

            // Set your license
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'moradi'
                    name 'Mehdi Moradi'
                    email 'moradi.mehdi@gmail.com'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
        // Add other pom properties here if you want (developer details / licenses)
        repository(url: "file://${localReleaseDest}")
    }
}

task zipRelease(type: Zip) {
    from localReleaseDest
    destinationDir buildDir
    archiveName "release-${sdkVersion}.zip"
}

task generateRelease << {
    println "Release ${sdkVersion} can be found at ${localReleaseDest}/"
    println "Release ${sdkVersion} zipped can be found ${buildDir}/release-${sdkVersion}.zip"
}

generateRelease.dependsOn(uploadArchives)
generateRelease.dependsOn(zipRelease)


artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}